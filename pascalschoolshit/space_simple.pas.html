<html>
							<head>
							  <meta http-equiv="Content-Type" content="text/html; charset=utf-8"> 

							  <!-- link rel="stylesheet" href="http://acm.math.spbu.ru/~sk1/colorer1/xcode.css">
							  <script src="http://acm.math.spbu.ru/~sk1/colorer1/highlight.pack.js"></script>
							  <script>hljs.initHighlightingOnLoad();</script -->

							  <link rel="stylesheet" title="Default" href="http://acm.math.spbu.ru/~sk1/colorer/my.css">
							  <script src="http://acm.math.spbu.ru/~sk1/colorer/highlight.js"></script>
							  <script src="http://acm.math.spbu.ru/~sk1/colorer/cpp.js"></script>
							  <script>hljs.initHighlightingOnLoad();</script>
							</head>

							<body>

							<pre><code>
uses graph, crt;

const h = 0.13;
const k = 10;

var
  D, m : integer;
  angle : real;
  starX, starY : array[1..1000] of integer;

{Получение координаты x в декартовой системе координат}
function getX(x : integer) : integer;
begin
  getX := x + 320;
end;

{Получение координаты y в декартовой системе координат}
function getY(y : integer) : integer;
begin
  getY := -y + 240;
end;

{Получение координаты x эллипса с углом t и параметром a}
function getPosX(t, a : real) : real;
begin
  getPosX := a * Cos(t);
end;

{Получение координаты y эллипса с углом t и параметром b}
function getPosY(t, b : real) : real;
begin
  getPosY := b * Sin(t);
end;

{Запись координат n звёзд в массив (по x в starX, по y в starY)}
procedure GetStars(n : integer);
var i : integer;
begin
  for i := 1 to n do
  begin
    starX[i] := Random(640);
    starY[i] := Random(480);
  end;
end;

{Рисуем n звезд}
procedure DrawStars(n : integer);
var i : integer;
begin
  for i := 1 to n do
    PutPixel(starX[i], starY[i], White);
end;

{Рисуем солнце с координатами (x, y) и радиусом r}
procedure DrawSun(x, y, r : integer; cl : Word);
var t, nx, ny : integer;
begin
  SetColor(cl);
  SetFillStyle(SolidFill, cl);
  FillEllipse(getX(x), getY(y), r, r);

  {Рисуем 12 лучиков от центра солнца (x, y) до точки на окружности (nx, ny)}
  for t := 0 to 11 do
  begin
    nx := Round(getPosX(t * 100, 80));
    ny := Round(getPosY(t * 100, 80));
    Line(getX(x), getY(y), getX(nx), getY(ny));
  end;
end;

{Рисуем Землю. Угол t, радиус r}
procedure DrawEarth(t : real; r : integer; cl : Word);
var x, y : integer;
begin
  SetColor(cl);
  SetFillStyle(SolidFill, cl);
  {Вычисляем координаты Земли (двигается по эллипсу)}
  x := Round(k * getPosX(t, 20));
  y := Round(k * getPosY(t, 15));
  FillEllipse(getX(x), getY(y), r, r);
end;

{Рисуем Луну. Угол t, радиус r}
procedure DrawMoon(t : real; r : integer; cl : Word);
var x, y : integer;
    xEarth, yEarth : real;
begin
  {Вычисляем текущие координаты Земли (xEarth, yEarth)}
  {Чтобы вычислить координаты Луны}
  xEarth := getPosX(t, 20);
  yEarth := getPosY(t, 15);
  SetColor(cl);
  SetFillStyle(SolidFill, cl);
  x := Round(k * (xEarth + getPosX(t * 150, 5)));
  y := Round(k * (yEarth + getPosY(t * 150, 5)));
  FillEllipse(getX(x), getY(y), r, r);
end;

begin
  D := detect;
  InitGraph(D, m, '');
  Randomize;

  GetStars(1000);
  DrawSun(0, 0, 30, Yellow);
  angle := 0;
  repeat
    {Каждый раз заново рисуем звёзды, потому что те, что будут находится за Землей, будут ею "стираться"}
    DrawStars(1000);
    DrawEarth(angle, 15, Cyan);
    DrawMoon(angle, 5, White);

    Delay(120);

    {Закрашиваем черным цветом Землю и Луну, то есть стираем их}
    DrawEarth(angle, 15, Black);
    DrawMoon(angle, 5, Black);
    angle := angle + h;
  until KeyPressed;
end.
</code></pre>

							</body>
							</html>

							<font style="visibility:hidden">

